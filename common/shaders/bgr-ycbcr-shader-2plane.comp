#version 450

layout (binding = 0, rgba8) uniform readonly image2D inputImage;
layout (binding = 1, r8) uniform writeonly image2D outputImage0;
layout (binding = 2, rg8) uniform writeonly image2D outputImage1;

layout (local_size_x = 16, local_size_y = 16, local_size_z = 1) in;

layout(push_constant) uniform PushConstants {
    uvec2 inputSize;
} pushConstants;

const mat4 rgb2ycbcr = mat4(
    0.299, -0.169,  0.5,   0,
    0.587, -0.331, -0.419, 0,
    0.114,  0.5,   -0.081, 0,
    0    ,  0.5,    0.5,   1
);

void main() {
    ivec2 gid = ivec2(gl_GlobalInvocationID.xy);
    if (gid.x >= int(pushConstants.inputSize.x) || gid.y >= int(pushConstants.inputSize.y)) {
        imageStore(outputImage0, gid, vec4(0.0));
        imageStore(outputImage1, ivec2(gid.x / 2, gid.y / 2), vec4(0.5, 0.5, 0.0, 0.0));
        return;
    }

    vec4 bgra = imageLoad(inputImage, gid);
    vec4 rgba = vec4(bgra.b, bgra.g, bgra.r, 1.0);

    vec4 ycbcra = rgb2ycbcr * rgba;
    
    imageStore(outputImage0, ivec2(gl_GlobalInvocationID.xy), vec4(ycbcra.x));
    imageStore(outputImage1, ivec2(gl_GlobalInvocationID.x / 2, gl_GlobalInvocationID.y / 2), vec4(ycbcra.y, ycbcra.z, 0.0, 0.0));
}
